plugins {
    id "com.avast.gradle.docker-compose" version "0.14.3"
    id "com.diffplug.spotless" version "6.20.0"
    id "org.sonarqube" version "4.2.1.3168"
    id "jacoco"
    id "java"
}

apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'com.avast.gradle.docker-compose'

group 'cricket.merstham'

ext {
    dependencyVersions = [
            spring_boot    : "2.7.8",
            spring_sessions: "2.7.0",
            apollo         : "2.5.13",
            prometheus     : "1.10.4",
            lombok         : "1.18.24",
            testcontainers : "1.16.2",
            jackson        : "2.13.2",
    ]
}

dockerCompose {
    buildBeforeUp = true
    forceRecreate = true
    startedServices = [
            'loki',
            'traefik',
            'postgres',
            'redis',
            'keycloak',
            'graph',
            'smtp',
            'prometheus',
            'web',
    ]

    def logDir = new File(project.buildDir, "logs")
    if (!logDir.exists()) {
        println("Creating logs folder...")
        logDir.mkdir()
    }
    captureContainersOutput = false
    captureContainersOutputToFile = new File('logs', 'docker-compose-gradle.log')
    projectName = "website"
}

spotless {
    java {
        target '**/*.java'
        googleJavaFormat('1.10.0').aosp()
        importOrder '', 'javax', 'java', '\\#'
    }

    sql {
        target 'sql/**/*.sql'

        dbeaver()
    }

    groovyGradle {
        target '**/*.gradle'
    }
}

repositories {
    mavenCentral()
}

sonarqube {
    properties {
        property "sonar.projectKey", "mersthamcc_website"
        property "sonar.organization", "mersthamcc"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    dependsOn "test"
}

tasks["sonarqube"].dependsOn test
